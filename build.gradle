ext {
    versions = [
            log4j  : "2.6.2",
            ratpack: '1.4.5',
            slf4j  : "1.7.21",
            spock  : "1.0-groovy-2.4",
    ]
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "io.ratpack:ratpack-gradle:1.4.5"
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

apply plugin: 'java'

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException("Must be built with Java 8 or higher")
}

apply plugin: "io.ratpack.ratpack-java"
apply plugin: "idea"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "docker"

repositories {
    jcenter()
}

dependencies {
    compile 'org.webjars.npm:api-console:3.0.4'
    compile 'org.webjars.npm:livereload-js:2.2.2'

    compile 'commons-cli:commons-cli:1.4'
    compile 'org.raml:raml-parser-2:1.0.7'
    compile ratpack.dependency('handlebars')

    compile 'org.assertj:assertj-core:3.6.2'

    runtime "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j}"
    runtime "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    runtime "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    runtime 'com.lmax:disruptor:3.3.2'

    compile 'net.incongru.watchservice:barbary-watchservice:1.0'

    testCompile "org.spockframework:spock-core:${versions.spock}"
}

mainClassName = "RambleApp"

run {
    if (project.hasProperty("cliArgs")) {
        args(cliArgs.split(","))
    }
}

task buildDocker(type: Docker) {
    baseImage = 'openjdk:8-jre-alpine'
    maintainer = 'Matthias KÃ¶ster "matthias.koester@gmail.com"'
    tag = 'vrap'
    addFile {
        from shadowJar
        rename {
            'vrap.jar'
        }
    }

    exposePort(5050)

    entryPoint(["java", "-jar", "/vrap.jar"])
}

buildDocker.dependsOn(shadowJar)